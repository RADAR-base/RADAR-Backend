plugins {
    // Apply the java plugin to add support for Java
    id 'java'
    id 'application'
    id 'com.jfrog.bintray' version '1.7.3' apply false

}

//---------------------------------------------------------------------------//
// Configuration                                                             //
//---------------------------------------------------------------------------//

group = 'org.radarcns'
version = '0.2.4-SNAPSHOT'

ext.description = 'Kafka backend for processing device data.'

mainClassName = 'org.radarcns.RadarBackend'
applicationDefaultJvmArgs = ["-Dlog4j.configuration=log4j.properties"]

ext.githubRepoName = 'RADAR-CNS/RADAR-Backend'

targetCompatibility = '1.8'
sourceCompatibility = '1.8'

ext.boundaryVersion = '1.0.6'
ext.codacyVersion = '1.0.10'
ext.confluentVersion = '3.3.1'
ext.hamcrestVersion = '1.3'
ext.kafkaVersion = '0.11.0.2'
ext.jacksonVersion='2.8.5'
ext.javaMailVersion = '1.5.6'
ext.junitVersion = '4.12'
ext.mathVersion = '3.0'
ext.findbugVersion = '3.0.1'
ext.commonsCliVersion = '1.2'
ext.mockitoVersion = '2.2.29'
ext.radarCommonsVersion = '0.8.2'
ext.radarSchemasVersion = '0.3.2'
ext.subethamailVersion = '3.1.7'
ext.jsoupVersion = '1.10.2'
ext.slf4jVersion = '1.7.25'
ext.log4jVersion = '1.2.17'
ext.avroVersion = '1.8.2'

ext.githubUrl = 'https://github.com/' + githubRepoName + '.git'
ext.issueUrl = 'https://github.com/' + githubRepoName + '/issues'
ext.website = 'http://radar-cns.org'

//---------------------------------------------------------------------------//
// Sources and classpath configurations                                        //
//---------------------------------------------------------------------------//

// In this section you declare where to find the dependencies of your project
repositories {
    jcenter()
    // Non-jcenter radar releases
    maven { url 'http://dl.bintray.com/radar-cns/org.radarcns' }
    // Kafka/confluent releases
    maven { url 'http://packages.confluent.io/maven/' }
    // For working with dev-branches
    maven { url 'http://oss.jfrog.org/artifactory/oss-snapshot-local/' }
    // Github code
    maven { url 'https://jitpack.io' }
}

// In this section you declare the dependencies for your production and test code
dependencies {
    compile group: 'org.radarcns', name: 'radar-commons', version: radarCommonsVersion
    compile group: 'org.apache.avro', name: 'avro', version: avroVersion
    compile group: 'org.radarcns', name: 'radar-commons-testing', version: radarCommonsVersion
    compile group: 'org.radarcns', name: 'radar-schemas-commons', version: radarSchemasVersion

    // Kafka streaming API
    compile group: 'org.apache.kafka', name: 'kafka-streams', version: kafkaVersion
    compile (group: 'io.confluent', name: 'kafka-streams-avro-serde', version: confluentVersion) {
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        exclude group: 'log4j', module: 'log4j'
        exclude group: 'jline', module: 'jline'
        exclude group: 'io.netty', module: 'netty'
    }

    // Nonnull annotation
    compile group: 'com.google.code.findbugs' , name: 'jsr305' , version: findbugVersion

    // Command-line interface
    compile group: 'commons-cli', name: 'commons-cli', version: commonsCliVersion

    // Configuration @JsonProperty
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: jacksonVersion

    // Monitor mail sending
    compile group: 'javax.mail', name: 'javax.mail-api', version: javaMailVersion
    compile group: 'com.sun.mail', name: 'javax.mail', version: javaMailVersion

    // JSoup html parser
    compile group: 'org.jsoup', name: 'jsoup', version: jsoupVersion

    runtimeOnly group: 'log4j', name: 'log4j', version: log4jVersion
    runtimeOnly group: 'org.slf4j', name: 'slf4j-log4j12', version: slf4jVersion
}

run {
    if ( project.hasProperty("appArgs") ) {
        args Eval.me(appArgs)
    }
}

if (!hasProperty('profile')) {
    ext.profile = 'dev'
}

apply from: "gradle/profile.${profile}.gradle"
