plugins {
    // Apply the java plugin to add support for Java
    id 'java'
    id 'application'
}

//---------------------------------------------------------------------------//
// Configuration                                                             //
//---------------------------------------------------------------------------//

group = 'org.radarcns'
version = '0.4.1-SNAPSHOT'

mainClassName = 'org.radarcns.RadarBackend'
applicationDefaultJvmArgs = ["-Dlog4j.configuration=log4j.properties"]


targetCompatibility = '11.0'
sourceCompatibility = '11.0'

ext {
    moduleDescription = 'Kafka backend for processing device data.'
    githubRepoName = 'RADAR-Base/RADAR-Backend'


    githubUrl = 'https://github.com/' + githubRepoName + '.git'
    issueUrl = 'https://github.com/' + githubRepoName + '/issues'
    website = 'http://radar-base.org'

    codacyVersion = '4.0.2'
    confluentVersion = '5.5.1'
    hamcrestVersion = '1.3'
    kafkaVersion = '2.5.0'
    jacksonVersion = '2.11.2'
    javaMailVersion = '1.6.2'
    junitVersion = '4.12'
    findbugVersion = '3.0.2'
    commonsCliVersion = '1.4'
    mockitoVersion = '3.5.11'
    radarCommonsVersion = '0.13.2'
    radarSchemasVersion = '0.7.4'
    subethamailVersion = '3.1.7'
    jsoupVersion = '1.13.1'
    slf4jVersion = '1.7.30'
    log4jVersion = '1.2.17'
    avroVersion = '1.9.2'
    jsonPathVersion = '2.6.0'
}

//---------------------------------------------------------------------------//
// Sources and classpath configurations                                        //
//---------------------------------------------------------------------------//

// In this section you declare where to find the dependencies of your project
repositories {
    mavenCentral()
    // Kafka/confluent releases
    maven { url 'http://packages.confluent.io/maven/' }
    // For working with dev-branches
    maven { url 'http://oss.jfrog.org/artifactory/oss-snapshot-local/' }
    // Github code
    maven { url 'https://jitpack.io' }
}

// In this section you declare the dependencies for your production and test code
dependencies {
    implementation group: 'org.radarbase', name: 'radar-commons', version: radarCommonsVersion
    implementation group: 'org.apache.avro', name: 'avro', version: avroVersion
    implementation group: 'org.radarbase', name: 'radar-commons-testing', version: radarCommonsVersion
    implementation group: 'org.radarbase', name: 'radar-schemas-commons', version: radarSchemasVersion
    implementation group: 'org.radarbase', name: 'oauth-client-util', version: '0.8.0'

    // Kafka streaming API
    implementation group: 'org.apache.kafka', name: 'kafka-streams', version: kafkaVersion
    implementation group: 'io.confluent', name: 'kafka-streams-avro-serde', version: confluentVersion

    // Nonnull annotation
    implementation group: 'com.google.code.findbugs' , name: 'jsr305' , version: findbugVersion

    // Command-line interface
    implementation group: 'commons-cli', name: 'commons-cli', version: commonsCliVersion

    // Configuration @JsonProperty
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: jacksonVersion
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jacksonVersion

    // Monitor mail sending
    implementation group: 'javax.mail', name: 'javax.mail-api', version: javaMailVersion
    implementation group: 'com.sun.mail', name: 'javax.mail', version: javaMailVersion

    // JSoup html parser
    implementation group: 'org.jsoup', name: 'jsoup', version: jsoupVersion

    // JsonPath for evaluating json conditions dynamically.
    implementation group: 'com.jayway.jsonpath', name: 'json-path', version: jsonPathVersion

    runtimeOnly group: 'org.radarbase', name: 'radar-commons-unsafe', version: radarCommonsVersion

    runtimeOnly group: 'log4j', name: 'log4j', version: log4jVersion
    runtimeOnly group: 'org.slf4j', name: 'slf4j-log4j12', version: slf4jVersion
}

if (!hasProperty('profile')) {
    ext.profile = 'dev'
}

apply from: "gradle/profile.${profile}.gradle"
